# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("url_mojom_gurl") {
  generate_java = true
  sources = [ "url.mojom" ]
  cpp_typemaps = [ ":url_typemap" ]
  blink_cpp_typemaps =
      [ "//third_party/blink/renderer/platform/mojo:url_typemap" ]
}

mojom("url_mojom_origin") {
  generate_java = true
  sources = [ "origin.mojom" ]

  public_deps = [
    ":url_mojom_gurl",
    "//mojo/public/mojom/base",
  ]

  check_includes_blink = false

  cpp_typemaps = [ ":origin_typemap" ]
  blink_cpp_typemaps =
      [ "//third_party/blink/renderer/platform/mojo:origin_typemap" ]
}

mojom("test_url_mojom_gurl") {
  sources = [ "url_test.mojom" ]

  public_deps = [
    ":url_mojom_gurl",
    ":url_mojom_origin",
  ]
}

component("mojom_traits") {
  output_name = "url_mojom_traits"

  sources = [
    "origin_mojom_traits.cc",
    "origin_mojom_traits.h",
    "url_gurl_mojom_traits.cc",
    "url_gurl_mojom_traits.h",
  ]

  defines = [ "IS_URL_MOJOM_TRAITS_IMPL" ]

  public_deps = [
    ":url_mojom_gurl_shared",
    ":url_mojom_origin_shared",
    "//base",
    "//mojo/public/cpp/base:shared_typemap_traits",
    "//url",
  ]
}

mojom_cpp_typemap("url_typemap") {
  types = [
    {
      mojom = "url.mojom.Url"
      cpp = "::GURL"
    },
  ]
  public_headers = [
    "//url/gurl.h",
    "//url/mojom/url_gurl_mojom_traits.h",
  ]
  public_deps = [
    ":mojom_traits",
    "//url",
  ]
}

mojom_cpp_typemap("origin_typemap") {
  types = [
    {
      mojom = "url.mojom.Origin"
      cpp = "::url::Origin"
    },
  ]
  public_headers = [
    "//url/origin.h",
    "//url/mojom/origin_mojom_traits.h",
  ]
  public_deps = [
    ":mojom_traits",
    "//url",
  ]
}
